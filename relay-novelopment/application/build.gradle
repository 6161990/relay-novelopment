plugins {
    id 'java'
    id 'io.freefair.lombok' version "6.5.0-rc1"
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.hidetake.swagger.generator' version '2.19.2'
}

group 'com.yoon'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation project(':domain')
    implementation project(':infrastructure')
    implementation 'org.valid4j:valid4j:0.5.0'

    implementation 'org.springdoc:springdoc-openapi-ui:1.6.14'
    implementation 'org.springdoc:springdoc-openapi-javadoc:1.6.14'
    implementation 'io.swagger:swagger-annotations:1.6.9'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.36'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'


    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.6.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:2.7.2'
    annotationProcessor 'com.github.therapi:therapi-runtime-javadoc-scribe:0.15.0'
}

test {
    useJUnitPlatform()
}

swaggerSources {
    openapi {
        inputFile = file("$rootDir/openapi.yaml")
        code {
            language = 'spring'
            configFile = file("$rootDir/config.json")
            components = ['models', 'apis']
            jvmArgs = ['--add-opens=java.base/java.util=ALL-UNNAMED']
        }
    }
}

compileJava.dependsOn swaggerSources.openapi.code
sourceSets.main.java.srcDir "${swaggerSources.openapi.code.outputDir}/src/main/java"
sourceSets.main.resources.srcDir "${swaggerSources.openapi.code.outputDir}/src/main/resources"
